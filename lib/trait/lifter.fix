module Minilib.Trait.Lifter;

import Minilib.Monad.Identity;
import Minilib.Monad.IO;

// A trait which converts a value of a type to a value of another type.
trait lf: Lifter {
    // A type that the `lifter` converts from.
    type LiftFrom lf;
    // A type that the `lifter` converts to.
    type LiftTo lf;
    // A singleton instance of the lifter.
    // This instance is required to select the implementation of `Lifter` trait.
    lifter: lf;
    // Converts a value of type `LiftFrom lf` to a value of type `LiftTo lf`.
    lift_from: (LiftFrom lf) -> lf -> (LiftTo lf);
}

// An empty structure for `Lifter` implementation.
type [n: * -> *, m: * -> *] LifterImpl from to = unbox struct {};

impl [m: Monad] LifterImpl (Identity a) (m a): Lifter {
    type LiftFrom (LifterImpl (Identity a) (m a)) = Identity a;
    type LiftTo (LifterImpl (Identity a) (m a)) = m a;
    lifter = LifterImpl{};
    lift_from = |ma, _| pure $ ma.Identity::get;
}

impl [m: MonadIO] LifterImpl (IO a) (m a): Lifter {
    type LiftFrom (LifterImpl (IO a) (m a)) = IO a;
    type LiftTo (LifterImpl (IO a) (m a)) = m a;
    lifter = LifterImpl{};
    lift_from = |ma, _| ma.lift_io;
}

impl [m: MonadIOFail] LifterImpl (IOFail a) (m a): Lifter {
    type LiftFrom (LifterImpl (IOFail a) (m a)) = IOFail a;
    type LiftTo (LifterImpl (IOFail a) (m a)) = m a;
    lifter = LifterImpl{};
    lift_from = |ma, _| ma.lift_iofail;
}
